buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.17'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'base'

subprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        header = rootProject.file('LICENSE')
        skipExistingHeaders = false
        mapping {
            java = 'SLASHSTAR_STYLE'
            fxml = 'XML_STYLE'
        }
        excludes(['**/*.yml'])
    }
}

configure([project(':server')]) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    repositories {
        jcenter()
    }
    
    dependencies {
        // Spring Boot
        compile 'org.springframework.boot:spring-boot-starter-web'

        // Spring Cloud Connectors
        compile "org.springframework.cloud:spring-cloud-cloudfoundry-connector:1.2.3.RELEASE"
        compile "org.springframework.cloud:spring-cloud-spring-service-connector:1.2.3.RELEASE"

        // JSON
        compile 'org.glassfish:javax.json:1.0.4'

        // Webjars
        compile "org.webjars:bootstrap:3.3.7-1"
        compile "org.webjars:jquery:3.1.1"
    }

    jar {
        // omit the version from the jar file name
        version = ''
    }

}

configure([project(':client')]) {
    apply plugin: 'org.javafxports.jfxmobile'
    applicationName = 'MessageOfTheDay'

    repositories {
        jcenter()
        maven {
            url 'https://nexus.gluonhq.com/nexus/content/repositories/releases'
        }
    }

    dependencies {
        compile 'com.gluonhq:glisten-afterburner:1.4.1'
        compile 'com.gluonhq:charm-cloudlink-client:4.5.2'
    }

    mainClassName = 'com.gluonhq.pivnet.motd.client.MessageOfTheDay'

    jfxmobile {
        downConfig {
            version = '3.8.6'
            plugins 'display', 'lifecycle', 'statusbar', 'storage'
        }
        android {
            manifest = 'src/android/AndroidManifest.xml'
        }
        ios {
            infoPList = file('src/ios/Default-Info.plist')
            forceLinkClasses = [
                    'com.gluonhq.pivnet.motd.**.*',
                    'com.gluonhq.**.*',
                    'javax.annotations.**.*',
                    'javax.inject.**.*',
                    'javax.json.**.*',
                    'org.glassfish.json.**.*'
            ]
        }
    }
}


